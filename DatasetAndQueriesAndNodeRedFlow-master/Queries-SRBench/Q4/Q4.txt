REGISTER QUERY GetObservation AS 
PREFIX om-owl: <http://knoesis.wright.edu/ssw/ont/sensor-observation.owl#>
PREFIX weather: <http://knoesis.wright.edu/ssw/ont/weather.owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?sensor (AVG(?windSpeed) AS ?averageWindSpeed)
               (AVG(?temperature) AS ?averageTemperature)
FROM NAMED STREAM <http://www.cwi.nl/SRBench/observations>
            [RANGE 1m TUMBLING]
WHERE {     
  ?temperatureObservation om-owl:procedure ?sensor .
  ?temperatureObservation a weather:TemperatureObservation .
  ?temperatureObservation  om-owl:result ?temperatureResult .
  ?temperatureResult om-owl:floatValue ?temperature .
  ?temperatureResult om-owl:uom ?uom .
  FILTER(?temperature > "32"^^xsd:float && REGEX(STR(?uom), "fahrenheit", "i"))
  ?windSpeedObservation om-owl:procedure ?sensor .
  ?windSpeedObservation a weather:WindSpeedObservation .
  ?windSpeedObservation om-owl:result ?result .
  ?result om-owl:floatValue ?windSpeed  .
}
GROUP BY ?sensor